CMAKE_MINIMUM_REQUIRED(VERSION 3.0)
SET(CMAKE_ALLOW_LOOSE_LOOP_CONSTRUCTS true)
# This line has to appear before 'PROJECT' in order to be able to disable incremental linking
SET(MSVC_INCREMENTAL_DEFAULT ON)

FILE(STRINGS "VERSION" BULLET_VERSION)
PROJECT(BULLET_PHYSICS VERSION "${BULLET_VERSION}")

LIST(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/build3/cmake")
INCLUDE(Bullet3Utils)

INCLUDE(GNUInstallDirs)
SET(BULLET_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}" CACHE PATH "Bullet install prefix")
SET(BULLET_INSTALL_INCLUDEDIR "${CMAKE_INSTALL_INCLUDEDIR}/bullet" CACHE PATH "Bullet install include folder")
SET(BULLET_INSTALL_BINDIR "${CMAKE_INSTALL_BINDIR}" CACHE PATH "Bullet install binary folder")
SET(BULLET_INSTALL_LIBDIR "${CMAKE_INSTALL_LIBDIR}" CACHE PATH "Bullet install library folder")
SET(BULLET_INSTALL_PKGCONFIGDIR "${CMAKE_INSTALL_LIBDIR}/pkgconfig" CACHE PATH "Bullet install pkgconfig folder")
SET(BULLET_INSTALL_CMAKEDIR "${CMAKE_INSTALL_LIBDIR}/cmake/bullet" CACHE PATH "Bullet install cmake scripts folder")

IF(NOT CMAKE_BUILD_TYPE)
	SET(CMAKE_BUILD_TYPE "Release")
ENDIF()

SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -D_DEBUG")

OPTION(USE_DOUBLE_PRECISION "Use double precision" OFF)
OPTION(BUILD_SHARED_LIBS "Use shared libraries" OFF)
OPTION(USE_SOFT_BODY_MULTI_BODY_DYNAMICS_WORLD "Use btSoftMultiBodyDynamicsWorld" ON)

OPTION(BULLET2_MULTITHREADING "Build Bullet 2 libraries with mutex locking around certain operations (required for multi-threading)" OFF)
IF(BULLET2_MULTITHREADING)
	OPTION(BULLET2_USE_OPEN_MP_MULTITHREADING "Build Bullet 2 with support for multi-threading with OpenMP (requires a compiler with OpenMP support)" OFF)
	OPTION(BULLET2_USE_TBB_MULTITHREADING "Build Bullet 2 with support for multi-threading with Intel Threading Building Blocks (requires the TBB library to be already installed)" OFF)
	IF(MSVC)
		OPTION(BULLET2_USE_PPL_MULTITHREADING "Build Bullet 2 with support for multi-threading with Microsoft Parallel Patterns Library (requires MSVC compiler)" OFF)
	ENDIF()
ENDIF()

IF("${CMAKE_GENERATOR}" MATCHES "Unix Makefiles")
	OPTION(INSTALL_LIBS "Set when you want to install libraries" ON)
ELSE()
	IF(APPLE AND FRAMEWORK)
		OPTION(INSTALL_LIBS "Set when you want to install libraries" ON)
	ELSE()
		# By default, don't enable the 'INSTALL' option for Xcode and MSVC projectfiles
		OPTION(INSTALL_LIBS "Set when you want to install libraries" OFF)
	ENDIF()
ENDIF()

IF(NOT WIN32)
	SET(DL ${CMAKE_DL_LIBS})
	IF(CMAKE_SYSTEM_NAME MATCHES "Linux")
		MESSAGE(STATUS "Linux detected")
		SET(OSDEF -D_LINUX)
	ELSE()
		IF(APPLE)
			MESSAGE(STATUS "Apple detected")
			SET(OSDEF -D_DARWIN)
		ELSE()
			MESSAGE(STATUS "BSD detected")
			SET(OSDEF -D_BSD)
		ENDIF()
	ENDIF()
ENDIF()

OPTION(USE_MSVC_INCREMENTAL_LINKING "Use MSVC Incremental Linking" OFF)
OPTION(USE_CUSTOM_VECTOR_MATH "Use custom vectormath library" OFF)

# statically linking VC++ isn't supported for WindowsPhone/WindowsStore
IF(CMAKE_SYSTEM_NAME STREQUAL WindowsPhone OR CMAKE_SYSTEM_NAME STREQUAL WindowsStore)
	OPTION(USE_MSVC_RUNTIME_LIBRARY_DLL "Use MSVC Runtime Library DLL (/MD or /MDd)" ON)
ELSE()
	OPTION(USE_MSVC_RUNTIME_LIBRARY_DLL "Use MSVC Runtime Library DLL (/MD or /MDd)" OFF)
ENDIF()

# SET(CMAKE_EXE_LINKER_FLAGS_INIT "/STACK:10000000 /INCREMENTAL:NO")
# SET(CMAKE_EXE_LINKER_FLAGS "/STACK:10000000 /INCREMENTAL:NO")

IF(MSVC)
	IF(NOT USE_MSVC_INCREMENTAL_LINKING)
		# MESSAGE("MSVC_INCREMENTAL_DEFAULT"+${MSVC_INCREMENTAL_DEFAULT})
		SET(MSVC_INCREMENTAL_YES_FLAG "/INCREMENTAL")
		SET(MSVC_INCREMENTAL_NO_FLAG "/INCREMENTAL:NO")

		STRING(REPLACE "${MSVC_INCREMENTAL_YES_FLAG}" "${MSVC_INCREMENTAL_NO_FLAG}" replacementFlags ${CMAKE_EXE_LINKER_FLAGS_DEBUG})
		SET(CMAKE_EXE_LINKER_FLAGS_DEBUG "${replacementFlags} ${MSVC_INCREMENTAL_NO_FLAG}")
		MESSAGE("CMAKE_EXE_LINKER_FLAGS_DEBUG=${CMAKE_EXE_LINKER_FLAGS_DEBUG}")

		STRING(REPLACE "${MSVC_INCREMENTAL_YES_FLAG}" "${MSVC_INCREMENTAL_NO_FLAG}" replacementFlags4 "${CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO}")
		SET(CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO "${replacementFlags4} ${MSVC_INCREMENTAL_NO_FLAG}")

		STRING(REPLACE "${MSVC_INCREMENTAL_YES_FLAG}" "${MSVC_INCREMENTAL_NO_FLAG}" replacementFlags2 ${CMAKE_EXE_LINKER_FLAGS})
		SET(CMAKE_EXE_LINKER_FLAGS "${replacementFlags2} ${MSVC_INCREMENTAL_NO_FLAG}")
	ENDIF()

	IF(NOT USE_MSVC_RUNTIME_LIBRARY_DLL)
		# We statically link to reduce dependencies
		FOREACH(flag_var CMAKE_CXX_FLAGS CMAKE_CXX_FLAGS_DEBUG CMAKE_CXX_FLAGS_RELEASE CMAKE_CXX_FLAGS_MINSIZEREL CMAKE_CXX_FLAGS_RELWITHDEBINFO CMAKE_C_FLAGS CMAKE_C_FLAGS_DEBUG CMAKE_C_FLAGS_RELEASE CMAKE_C_FLAGS_MINSIZEREL CMAKE_C_FLAGS_RELWITHDEBINFO )
			IF(${flag_var} MATCHES "/MD")
				STRING(REGEX REPLACE "/MD" "/MT" ${flag_var} "${${flag_var}}")
			ENDIF()
			IF(${flag_var} MATCHES "/MDd")
				STRING(REGEX REPLACE "/MDd" "/MTd" ${flag_var} "${${flag_var}}")
			ENDIF()
		ENDFOREACH()
	ENDIF()

	IF(CMAKE_CL_64)
		ADD_DEFINITIONS(-D_WIN64)
	ELSE()
		OPTION(USE_MSVC_SSE "Use MSVC /arch:sse option" OFF)
		OPTION(USE_MSVC_SSE2 "Compile your program with SSE2 instructions" ON)

		IF(USE_MSVC_SSE)
			SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /arch:SSE")
		ENDIF()
		IF(USE_MSVC_SSE2)
			SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /arch:SSE2")
		ENDIF()
	ENDIF()

	OPTION(USE_MSVC_AVX "Compile your program with AVX instructions" OFF)

	IF(USE_MSVC_AVX)
		ADD_DEFINITIONS(/arch:AVX)
	ENDIF()

	OPTION(USE_MSVC_FAST_FLOATINGPOINT "Use MSVC /fp:fast option" ON)
	IF(USE_MSVC_FAST_FLOATINGPOINT)
		SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /fp:fast")
	ENDIF()

	OPTION(USE_MSVC_STRING_POOLING "Use MSVC /GF string pooling option" ON)
	IF(USE_MSVC_STRING_POOLING)
		SET(CMAKE_C_FLAGS "/GF ${CMAKE_C_FLAGS}")
		SET(CMAKE_CXX_FLAGS "/GF ${CMAKE_CXX_FLAGS}")
	ENDIF()

	OPTION(USE_MSVC_FUNCTION_LEVEL_LINKING "Use MSVC /Gy function level linking option" ON)
	IF(USE_MSVC_FUNCTION_LEVEL_LINKING)
		SET(CMAKE_C_FLAGS "/Gy ${CMAKE_C_FLAGS}")
		SET(CMAKE_CXX_FLAGS "/Gy ${CMAKE_CXX_FLAGS}")
		SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} /OPT:REF")
		SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /OPT:REF")
	ENDIF()

	OPTION(USE_MSVC_EXEPTIONS "Use MSVC C++ exceptions option" OFF)

	OPTION(USE_MSVC_COMDAT_FOLDING "Use MSVC /OPT:ICF COMDAT folding option" ON)

	IF(USE_MSVC_COMDAT_FOLDING)
		SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} /OPT:ICF")
		SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /OPT:ICF")
	ENDIF()

	OPTION(USE_MSVC_DISABLE_RTTI "Use MSVC /GR- disabled RTTI flags option" ON)
	IF(USE_MSVC_DISABLE_RTTI)
		STRING(REGEX REPLACE "/GR" "" CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS}) # Disable RTTI
		SET(CMAKE_C_FLAGS "/GR- ${CMAKE_C_FLAGS}")
		SET(CMAKE_CXX_FLAGS "/GR- ${CMAKE_CXX_FLAGS}")
	ENDIF()

	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /wd4244 /wd4267")
ENDIF()

IF(WIN32)
	OPTION(INTERNAL_CREATE_DISTRIBUTABLE_MSVC_PROJECTFILES "Create MSVC projectfiles that can be distributed" OFF)

	IF(INTERNAL_CREATE_DISTRIBUTABLE_MSVC_PROJECTFILES)
		SET(LIBRARY_OUTPUT_PATH ${BULLET_PHYSICS_SOURCE_DIR}/lib CACHE PATH "Single output directory for building all libraries.")
		SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${BULLET_PHYSICS_SOURCE_DIR})
		SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG ${BULLET_PHYSICS_SOURCE_DIR})
		SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE ${BULLET_PHYSICS_SOURCE_DIR})
		SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${BULLET_PHYSICS_SOURCE_DIR})
		SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${BULLET_PHYSICS_SOURCE_DIR})
	ELSE()
		SET(LIBRARY_OUTPUT_PATH ${CMAKE_BINARY_DIR}/lib CACHE PATH "Single output directory for building all libraries.")
		SET(EXECUTABLE_OUTPUT_PATH ${CMAKE_BINARY_DIR}/lib CACHE PATH "Single output directory for building all executables.")
	ENDIF()

	OPTION(INTERNAL_CREATE_MSVC_RELATIVE_PATH_PROJECTFILES "Create MSVC projectfiles with relative paths" OFF)
	OPTION(INTERNAL_ADD_POSTFIX_EXECUTABLE_NAMES "Add MSVC postfix for executable names (_Debug)" OFF)

	SET(CMAKE_DEBUG_POSTFIX "_Debug" CACHE STRING "Adds a postfix for debug-built libraries.")
	SET(CMAKE_MINSIZEREL_POSTFIX "_MinsizeRel" CACHE STRING "Adds a postfix for MinsizeRelease-built libraries.")
	SET(CMAKE_RELWITHDEBINFO_POSTFIX "_RelWithDebugInfo" CACHE STRING "Adds a postfix for ReleaseWithDebug-built libraries.")

	IF(INTERNAL_CREATE_MSVC_RELATIVE_PATH_PROJECTFILES)
		SET(CMAKE_SUPPRESS_REGENERATION 1)
		SET(CMAKE_USE_RELATIVE_PATHS 1)
	ENDIF()
ENDIF()

OPTION(BUILD_CPU_DEMOS "Build original Bullet CPU examples" ON)

OPTION(INTERNAL_UPDATE_SERIALIZATION_STRUCTURES "Internal update serialization structures" OFF)
IF(INTERNAL_UPDATE_SERIALIZATION_STRUCTURES)
	ADD_DEFINITIONS(-DBT_INTERNAL_UPDATE_SERIALIZATION_STRUCTURES)
ENDIF()

ADD_LIBRARY(BulletInterface INTERFACE)
BULLET3_INSTALL(TARGETS BulletInterface EXPORT BulletLibsExport)
IF(USE_DOUBLE_PRECISION)
	TARGET_COMPILE_DEFINITIONS(BulletInterface INTERFACE BT_USE_DOUBLE_PRECISION)
	SET(BULLET_INSTALL_DEFINITIONS "-DBT_USE_DOUBLE_PRECISION ${BULLET_INSTALL_DEFINITIONS}")
ENDIF()

IF(NOT USE_SOFT_BODY_MULTI_BODY_DYNAMICS_WORLD)
	ADD_DEFINITIONS(-DSKIP_SOFT_BODY_MULTI_BODY_DYNAMICS_WORLD)
ENDIF()

IF(BULLET2_MULTITHREADING)
	ADD_DEFINITIONS(-DBT_THREADSAFE=1 )
	IF(NOT MSVC)
		SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
	ENDIF()
	IF(NOT WIN32)
		LINK_LIBRARIES(pthread )
	ENDIF()
ENDIF()

IF(BULLET2_USE_OPEN_MP_MULTITHREADING)
	ADD_DEFINITIONS("-DBT_USE_OPENMP=1")
	IF(MSVC)
		SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /openmp")
	ELSE()
		# GCC, Clang
		SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fopenmp")
	ENDIF()
ENDIF()

IF(BULLET2_USE_TBB_MULTITHREADING)
	SET(BULLET2_TBB_INCLUDE_DIR "not found" CACHE PATH "Directory for Intel TBB includes.")
	SET(BULLET2_TBB_LIB_DIR "not found" CACHE PATH "Directory for Intel TBB libraries.")
	FIND_LIBRARY(TBB_LIBRARY tbb PATHS ${BULLET2_TBB_LIB_DIR})
	FIND_LIBRARY(TBBMALLOC_LIBRARY tbbmalloc PATHS ${BULLET2_TBB_LIB_DIR})
	ADD_DEFINITIONS("-DBT_USE_TBB=1")
	INCLUDE_DIRECTORIES(${BULLET2_TBB_INCLUDE_DIR} )
	LINK_LIBRARIES(${TBB_LIBRARY} ${TBBMALLOC_LIBRARY} )
ENDIF()

IF(BULLET2_USE_PPL_MULTITHREADING)
	ADD_DEFINITIONS("-DBT_USE_PPL=1")
ENDIF()

IF(WIN32)
	OPTION(USE_GLUT "Use Glut" ON)
	ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS )
	ADD_DEFINITIONS(-D_CRT_SECURE_NO_DEPRECATE )
	ADD_DEFINITIONS(-D_SCL_SECURE_NO_WARNINGS )

	IF(USE_GLUT AND MSVC)
		STRING(REPLACE "/D_WINDOWS" "" CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS})
		REMOVE_DEFINITIONS(-D_WINDOWS )
	ENDIF()
ELSE()
	OPTION(USE_GLUT "Use Glut" ON)
ENDIF()

# This is the shortcut to finding GLU, GLUT and OpenGL if they are properly installed on your system
# This should be the case.

SET(OpenGL_GL_PREFERENCE GLVND)
FIND_PACKAGE(OpenGL)
IF(OPENGL_FOUND)
	MESSAGE(STATUS "Found OpenGL: ${OPENGL_LIBRARIES}")
ELSE()
	MESSAGE("OPENGL NOT FOUND")
	SET(OPENGL_gl_LIBRARY opengl32)
	SET(OPENGL_glu_LIBRARY glu32)
ENDIF()

IF(APPLE)
	FIND_LIBRARY(COCOA_LIBRARY Cocoa)
ENDIF()

OPTION(BUILD_BULLET3 "Set when you want to build Bullet 3" ON)

# Optional Python configuration
# Will not probe environment for Python configuration (which can abort the
# build process) unless you explicitly turn on BUILD_PYBULLET.
OPTION(BUILD_PYBULLET "Set when you want to build pybullet (Python bindings for Bullet)" OFF)
IF(BUILD_PYBULLET)
	SET(PYTHON_VERSION_PYBULLET "" CACHE STRING "Python version pybullet will use.")
	SET(Python_ADDITIONAL_VERSIONS 3 3.6 3.5 3.4 3.3 3.2 3.1 3.0 2.7 2.7.12 2.7.10 2.7.3 )
	SET_PROPERTY(CACHE PYTHON_VERSION_PYBULLET PROPERTY STRINGS ${Python_ADDITIONAL_VERSIONS})
	OPTION(EXACT_PYTHON_VERSION "Require Python and match PYTHON_VERSION_PYBULLET exactly, e.g. 2.7.12" OFF)
	IF(EXACT_PYTHON_VERSION)
		SET(EXACT_PYTHON_VERSION_FLAG EXACT REQUIRED)
	ENDIF()
	# first find the python interpreter
	FIND_PACKAGE(PythonInterp ${PYTHON_VERSION_PYBULLET} ${EXACT_PYTHON_VERSION_FLAG})
	# python library should exactly match that of the interpreter
	# the following can result in fatal error if you don't have the right python configuration
	FIND_PACKAGE(PythonLibs ${PYTHON_VERSION_STRING} EXACT)
ENDIF()

OPTION(BUILD_ENET "Set when you want to build apps with enet UDP networking support" ON)
OPTION(BUILD_CLSOCKET "Set when you want to build apps with enet TCP networking support" ON)

IF(BUILD_PYBULLET)
	FIND_PACKAGE(PythonLibs)

	OPTION(BUILD_PYBULLET_NUMPY "Set when you want to build pybullet with NumPy support" OFF)
	OPTION(BUILD_PYBULLET_ENET "Set when you want to build pybullet with enet UDP networking support" ON)
	OPTION(BUILD_PYBULLET_CLSOCKET "Set when you want to build pybullet with enet TCP networking support" ON)

	OPTION(BUILD_PYBULLET_MAC_USE_PYTHON_FRAMEWORK "Set when you want to use the Python Framework on Mac" OFF)

	IF(BUILD_PYBULLET_NUMPY)
		# INCLUDE(FindNumPy)
		FIND_PACKAGE(NumPy)
		IF(PYTHON_NUMPY_FOUND)
			MESSAGE(STATUS "NumPy Found")
			ADD_DEFINITIONS(-DPYBULLET_USE_NUMPY)
		ELSE()
			MESSAGE(WARNING "NumPy NOT FOUND")
		ENDIF()
	ENDIF()

	IF(WIN32)
		SET(BUILD_SHARED_LIBS OFF CACHE BOOL "Shared Libs" FORCE)
	ELSE()
		SET(BUILD_SHARED_LIBS ON CACHE BOOL "Shared Libs" FORCE)
	ENDIF()

	IF(APPLE)
		OPTION(BUILD_PYBULLET_MAC_USE_PYTHON_FRAMEWORK "Set when you want to use the Python Framework on Mac" ON)
		IF(NOT BUILD_PYBULLET_MAC_USE_PYTHON_FRAMEWORK)
			ADD_DEFINITIONS(-DB3_NO_PYTHON_FRAMEWORK)
		ENDIF()
		OPTION(BUILD_PYBULLET_SHOW_PY_VERSION "Set when you want to show the PY_MAJOR_VERSION and PY_MAJOR_VERSION using #pragme message." OFF)
		IF(BUILD_PYBULLET_SHOW_PY_VERSION)
			ADD_DEFINITIONS(-DB3_DUMP_PYTHON_VERSION)
		ENDIF()
	ENDIF()
ENDIF()

IF(BUILD_SHARED_LIBS)
	MESSAGE(STATUS "Building shared bullet libraries")

	TARGET_COMPILE_DEFINITIONS(BulletInterface INTERFACE "BULLET_SHARED")
	SET(BULLET_INSTALL_DEFINITIONS "-DBULLET_SHARED ${BULLET_INSTALL_DEFINITIONS}")

	SET(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
ELSE()
	MESSAGE(STATUS "Building static bullet libraries")
ENDIF()

IF(NOT WIN32 AND NOT APPLE)
	OPTION(BUILD_EGL "Build OpenGL/EGL" ON)
	IF(BUILD_EGL)
		ADD_DEFINITIONS(-DBT_USE_EGL)
	ENDIF()
ENDIF()

IF(BUILD_BULLET3)
	 IF(APPLE)
		MESSAGE(STATUS "Mac OSX Version is ${_CURRENT_OSX_VERSION}")
		IF(_CURRENT_OSX_VERSION VERSION_LESS 10.9)
			MESSAGE(WARNING "Mac OSX below 10.9 has no OpenGL 3 support so please disable the BUILD_OPENGL3_DEMOS option")
			# UNSET(BUILD_OPENGL3_DEMOS CACHE)

			OPTION(BUILD_OPENGL3_DEMOS "Set when you want to build the OpenGL3+ demos" OFF)
		ELSE()
			OPTION(BUILD_OPENGL3_DEMOS "Set when you want to build the OpenGL3+ demos" ON)
		ENDIF()
	ELSE()
		OPTION(BUILD_OPENGL3_DEMOS "Set when you want to build Bullet 3 OpenGL3+ demos" ON)
	ENDIF()
ELSE()
	UNSET(BUILD_OPENGL3_DEMOS CACHE)
	OPTION(BUILD_OPENGL3_DEMOS "Set when you want to build Bullet 3 OpenGL3+ demos" OFF)
ENDIF()
IF(BUILD_OPENGL3_DEMOS)
	IF(EXISTS ${BULLET_PHYSICS_SOURCE_DIR}/Demos3 AND IS_DIRECTORY ${BULLET_PHYSICS_SOURCE_DIR}/Demos3)
		SUBDIRS(Demos3)
	ENDIF()
ELSE()
	ADD_DEFINITIONS(-DNO_OPENGL3)
ENDIF()

OPTION(BUILD_BULLET2_DEMOS "Set when you want to build the Bullet 2 demos" ON)
IF(BUILD_BULLET2_DEMOS)
	IF(EXISTS ${BULLET_PHYSICS_SOURCE_DIR}/examples AND IS_DIRECTORY ${BULLET_PHYSICS_SOURCE_DIR}/examples)
		SUBDIRS(examples)
	ENDIF()
ENDIF()

OPTION(BUILD_EXTRAS "Set when you want to build the extras" ON)
IF(BUILD_EXTRAS)
	SUBDIRS(Extras)
ENDIF()

SUBDIRS(src)

IF(NOT MSVC)
	CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/bullet.pc.cmake ${CMAKE_CURRENT_BINARY_DIR}/bullet.pc @ONLY)
	BULLET3_INSTALL(
		FILES "${CMAKE_CURRENT_BINARY_DIR}/bullet.pc"
		DESTINATION "${BULLET_INSTALL_PKGCONFIGDIR}")
ENDIF()

OPTION(BUILD_UNIT_TESTS "Build Unit Tests" ON)

IF(BUILD_UNIT_TESTS)
	ENABLE_TESTING()
	SUBDIRS(test)
ENDIF()

SET(BULLET_INSTALL_USEFILE "${BULLET_INSTALL_CMAKEDIR}/UseBullet.cmake")
INCLUDE(CMakePackageConfigHelpers)
CONFIGURE_PACKAGE_CONFIG_FILE(BulletConfig.cmake.in "${CMAKE_CURRENT_BINARY_DIR}/BulletConfig.cmake"
    INSTALL_DESTINATION "${BULLET_INSTALL_CMAKEDIR}"
    PATH_VARS BULLET_INSTALL_PREFIX BULLET_INSTALL_INCLUDEDIR BULLET_INSTALL_LIBDIR BULLET_INSTALL_USEFILE BULLET_INSTALL_CMAKEDIR)
WRITE_BASIC_PACKAGE_VERSION_FILE("${CMAKE_CURRENT_BINARY_DIR}/BulletConfigVersion.cmake"
    VERSION "${BULLET_VERSION}" COMPATIBILITY AnyNewerVersion)

OPTION(INSTALL_CMAKE_FILES "Install generated CMake files" ON)

IF(INSTALL_CMAKE_FILES)
	BULLET3_INSTALL(FILES ${CMAKE_CURRENT_SOURCE_DIR}/UseBullet.cmake
		${CMAKE_CURRENT_BINARY_DIR}/BulletConfig.cmake
		${CMAKE_CURRENT_BINARY_DIR}/BulletConfigVersion.cmake
		DESTINATION ${BULLET_INSTALL_CMAKEDIR})
	BULLET3_INSTALL(EXPORT BulletLibsExport
		DESTINATION "${BULLET_INSTALL_CMAKEDIR}" FILE "BulletTargets.cmake"
		NAMESPACE Bullet::)
ENDIF()
